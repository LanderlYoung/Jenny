// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        pub = [
                publishVersion: '1.1.2',
                userOrg       : 'landerlyoung',
                groupId       : 'io.github.landerlyoung',
                desc          : 'Java/Android JNI glue/proxy code generator',
                website       : 'https://github.com/LanderlYoung/Jenny',
                licences      : ['Apache-2.0']
        ]

        kotlin_version = '1.4.10'

        configRepo = { repo ->
            repo.mavenLocal()
            repo.jcenter()
            repo.google()
        }
    }

    configRepo repositories

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.novoda:bintray-release:0.9.1'
        classpath 'com.android.tools.build:gradle:4.1.0'
    }
}

allprojects {
    // basic
    configRepo buildscript.repositories
    configRepo repositories
    version = pub.publishVersion
    description = pub.desc

    // publish
    final def publishProject = [
            "annotation", "compiler"
    ] as Set

    if (publishProject.contains(name)) {
        apply plugin: 'com.novoda.bintray-release'

        publish {
            publishVersion = version
            artifactId = "jenny-$name"
            userOrg = pub.userOrg
            groupId = pub.groupId
            desc = pub.desc
            website = pub.website
            licences = pub.licences
            override = true

            // account config from command line
            // -PbintrayUser=xxx
            // -PbintrayKey=xxx
            // -PdryRun=xxx
        }
    }
}
